--------------------------------------------------------------------------
--Formato del fichero
--------------------------------------------------------------------------
--P5
--fils cols
--max_gris
--pixels por filas (one byte per pixel, binary)
--------------------------------------------------------------------------
programa invertir_pgm
--------------------------------------------------------------------------
	entero formato; 
	entero fils, cols; 
	entero max_gris; 
    caracter[500000] image;
--------------------------------------------------------------------------
entero indice(entero fils, cols, i, j)
--------------------------------------------------------------------------
Principio
    indice := i*cols + j; 
Fin
--------------------------------------------------------------------------
booleano cargar_imagen(entero ref formato, fils, cols, prof;
         caracter[500000] ref imagen)
--------------------------------------------------------------------------
    caracter c; 
    entero i, j; 
Principio
    leer(c); 
    leer(formato); 
    leer(fils); 
    leer(cols); 
    leer(max_gris); 
    leer_lin(); 

    Sel 
        caso fils*cols > 500000:
            escribir_lin("Imagen de dimensiones (", fils, ", ", cols,
                    ") demasiado grande (max 500000 pixels)!");
            cargar_imagen := falso;
        dlc:
            i := 0; 
            Mq i < fils
                j := 0; 
                Mq j < cols
                    leer(c); 
                    image[indice(fils, cols, i, j)] := c; 
                    j := j + 1; 
                FMq
                i := i + 1; 
            FMq
            cargar_imagen := verdadero;
    FSel
Fin
--------------------------------------------------------------------------
guardar_imagen(entero formato, fils, cols, prof; caracter[500000] ref imagen)
--------------------------------------------------------------------------
    caracter c;
    entero i, j;
Principio
    escribir_lin ('P', formato); 
    escribir_lin (fils, ' ', cols); 
    escribir_lin (prof); 
    i := 0; 
    Mq i < fils
        j := 0; 
        Mq j < cols
            escribir(image[indice(fils, cols, i, j)]); 
            j := j + 1; 
        FMq
        i := i + 1; 
    FMq
Fin
--------------------------------------------------------------------------
invertir_imagen(entero formato, fils, cols, prof; caracter[500000] ref imagen)
--------------------------------------------------------------------------
    caracter c; 
    entero i, j, idx; 
Principio
    i := 0; 
    Mq i < fils
        j := 0; 
        Mq j < cols
            idx := indice(fils, cols, i, j); 
            image[idx] := entAcar(max_gris - carAent(image[idx])); 			   
            j := j + 1; 
        FMq
        i := i + 1; 
    FMq
Fin
---------------------------------------------------
Principio
	Sel 
        caso cargar_imagen(formato, fils, cols, max_gris, image):
            invertir_imagen(formato, fils, cols, max_gris, image); 
            guardar_imagen(formato, fils, cols, max_gris, image);
    FSel
Fin