/**
 * Orden de prioridad de expansión (cuanto más izquierda, más prioridad).
 * primario - factor - termino - expresion_simple - relacion - expresion
 */

AtribExp primario() : 
{
    Token t;
    AtribExp ae;
    Symbol s = null;
    SymbolFunction s_func;
    SymbolArray s_array;
}
{
    {
        System.out.println("Entrando en primario");
    }
    (
        /**
         * Caso: llamada a entAcar.
         */
        entAcar()
        { return new AtribExp(Symbol.Types.CHAR, null, false, false); }
    |    
        /**
         * Caso: llamada a carAent.
         */
        carAent()
        { return new AtribExp(Symbol.Types.INT, null, false, false); }
    |
        /**
         * Caso: llamada a función excluyendo procedimientos.
         */
        LOOKAHEAD(2) // Para distinguirlo de un array o variable.
        t = <tIDENTIFICADOR>
        <tOPENING_PARENTHESIS>
        parametros_llamada_funcion()
        <tCLOSING_PARENTHESIS>
        {
            try {
                s = st.getSymbol(t.image);
            } catch (SymbolNotFoundException e) {
                /**
                 * Lanzar una excepción si la sintaxis utilizada es la de la llamada a una función pero el identificador semánticamente no existe.
                 */
                System.err.println("Error: El identificador '" + t.image + "' no está definido.");
                return null;
            }
            /**
             * Lanzar una excepción si la sintaxis utilizada es la de la llamada a una función pero el identificador semánticamente no es una función.
             */
            if (s.type != Symbol.Types.FUNCTION) {
                System.err.println("Error: Se esperaba que '" + t.image + "' fuera una función (o procedimiento), pero se encontró: " + s.type + ".");
            }
            /**
             * Devolver una expresión del tipo de retorno de la función y valor indefinido (no evaluable estáticamente).
             */
            s_func = (SymbolFunction) s;
            return new AtribExp(s_func.returnType, null, false, false); // Marcar el atributo variable a falso porque las funciones devuelven constantes.
        }
    |
        /**
         * Caso: acceso a array.
         */
        LOOKAHEAD(2) // Para distinguirlo del acceso a una variable.
        t = <tIDENTIFICADOR>
        <tOPENING_SQUARE_BRACKET>
        ae = expresion()
        <tCLOSING_SQUARE_BRACKET>
        {
            try {
                s = st.getSymbol(t.image);
            } catch (SymbolNotFoundException e) {
                /**
                 * Lanzar una excepción si la sintaxis utilizada es la de un array pero el identificador semánticamente no existe.
                 */
                System.err.println("Error: El identificador '" + t.image + "' no está definido.");
                return null;
            }
            /**
             * Lanzar una excepción si la sintaxis utilizada es la de un array pero el identificador semánticamente no es un array.
             */
            if (s.type != Symbol.Types.ARRAY) {
                System.err.println("Error: Se esperaba que '" + t.image + "' fuera un array, pero se encontró: " + s.type + ".");
            }
            /**
             * Lanzar una excepción si el tipo del valor de acceso al array no es un entero.
             */
            if (ae.type != Symbol.Types.INT) {
                System.err.println("Error: Se esperaba que el índice del array fuera un entero, pero se evaluó: tipo " + ae.type + " con valor '" + ae.image + "'.");
            }
            s_array = (SymbolArray) s; // Para acceder al campo maxInd
            /**
             * Optimización: si el valor de acceso al vector es un entero y es constante, comprobar estáticamente que es válido.
             * Lanzar una excepción si no es válido.
             */
            if (ae.isConst && Integer.valueOf(ae.image) > s_array.maxInd) {
                System.err.println("Error: El índice " + ae.image + " es demasiado grande para el vector " + t.image + " de tamaño " + (s_array.maxInd + 1) + "'.");
            }
            /**
             * Lanzar una excepción si el valor de acceso al vector es un entero no constante.
             */
            Symbol.Types elemType = s_array.baseType;
            return new AtribExp(elemType, null, true, ae.isConst && s.constant);
        }
    |
        /**
         * Caso: acceso a una variable.
         */
        t = <tIDENTIFICADOR>
        {
            try {
                s = st.getSymbol(t.image);
                String image = t.image;
                return new AtribExp(s.type, null, true, false); // Valor null por no ser evaluable en tiempo de compilación (no existen variables constantes en gcl).
            } catch (SymbolNotFoundException e) {
                /**
                 * Lanzar una excepción si la sintaxis utilizada es la de acceso a una variable pero semánticamente el identificador no existe.
                 */
                System.err.println("Error: El identificador '" + t.image + "' no está definido.");
                throw new SymbolNotFoundException("Error: El identificador '" + t.image + "' no está definido.");
            }
        }
    |
        /**
         * Caso: constante int.
         */
        t = <tCONSTANT>
        { 
            return new AtribExp(Symbol.Types.INT, t.image, false, true); 
        }
    |
        /**
         * Caso: constante char.
         */
        t = <tCHAR>
        {
            return new AtribExp(Symbol.Types.CHAR, t.image, false, true); 
        }
    |
        /**
         * Caso: constante string.
         */
        t = <tSTRING>
        {
            return new AtribExp(Symbol.Types.STRING, t.image, false, true); 
        }
    |
        /**
         * Caso: constante bool con valor true.
         */
        t = <tTRUE>
        {
            return new AtribExp(Symbol.Types.BOOL, t.image, false, true); 
        }
    |
        /**
         * Caso: constante bool con valor false.
         */
        t = <tFALSE>
        {
            return new AtribExp(Symbol.Types.BOOL, t.image, false, true); 
        }
    )
    /**
     * Devolver una expresión de tipo no definido si no entra en ninguno de los casos válidos.
     */
    {
        System.err.println("Error: Saliendo de primario por caso no identificado (¿inaccesible?)");
        return new AtribExp(Symbol.Types.UNDEFINED, null, false, false);
    }
}

AtribExp factor() :
{
    System.out.println("Entrando en factor");
    Token op = null;
    AtribExp ae;
}
{
    (op = <tNOT> | op = <tPLUS> | op = <tMINUS>)?
    (
        <tOPENING_PARENTHESIS> ae = expresion() <tCLOSING_PARENTHESIS>
    |
        ae = primario()
    )
    {
        if (op != null) {
            /**
             * Lanzar una excepción si el operador no es válido.
             */
            if ((op.image.equals("!") && ae.type != Symbol.Types.BOOL) ||
                ((op.image.equals("+") || op.image.equals("-")) && ae.type != Symbol.Types.INT)) {
                System.out.println (
                    "Error: El operador '" + op.image + "' requiere un valor del tipo adecuado, pero se evaluó: "
                    + "tipo " + ae.type + " con valor '" + ae.image + "'."
                );
            }
            
            boolean isConst = ae.isConst;
            String val = null;
            
            /**
             * Evaluar la expresión recibida si es constante y tiene delante algún operador.
             */
            if (isConst) {
                if (op.image.equals("!")) {
                    val = String.valueOf(!Boolean.parseBoolean(ae.image));
                } else {
                    int num = Integer.parseInt(ae.image);
                    val = op.image.equals("-") ? String.valueOf(-num) : String.valueOf(num);
                }
            }
            
            /**
             * Devolver una expresión del tipo correspondiente, con el valor evaluado si es constante.
             */
            System.out.println("Saliendo de factor tras operador " + op.image + 
                               (isConst ? " con resultado constante" : " con resultado no constante"));
            return new AtribExp(op.image.equals("!") ? Symbol.Types.BOOL : Symbol.Types.INT, val, false, isConst);
        }
        
        /**
         * Devolver exactamente la expresión recibida si no tiene ningún operador delante, op == null.
         */
        System.out.println("Saliendo de factor con expresión primaria");
        return ae;
    }
}


AtribExp termino() :
{
    System.out.println("Entrando en termino");
    AtribExp left, right;
    Token op;
}
{
    left = factor()
    (
        op = operador_multiplicativo()
        right = factor()
        {
            /**
             * Lanzar una excepción si los 2 factores no son del mismo tipo.
             */
            if (left.type != Symbol.Types.INT || right.type != Symbol.Types.INT) {
                System.err.println(
                    "Error: Los operadores multiplicativos requieren valores enteros. Se evaluó: "
                    + "left.type = " + left.type + " con valor '" + left.image 
                    + "', right.type = " + right.type + " con valor '" + right.image + "'."
                );
            }
            boolean isConst = left.isConst && right.isConst;
            String val = null;
            
            /**
             * Evaluar el termino si los 2 factores son constantes.
             */
            if (isConst) {
                int l = Integer.parseInt(left.image);
                int r = Integer.parseInt(right.image);
                
                switch (op.image) {
                    case "*":
                        val = String.valueOf(l * r);
                        break;
                    case "/":
                        val = (r != 0) ? String.valueOf(l / r) : "0"; 
                        break;
                    case "MOD":
                        val = String.valueOf(l % r);
                        break;
                }
            }
            
            left = new AtribExp(Symbol.Types.INT, val, false, isConst);
        }
    )*
    { 
        System.out.println("Saliendo de termino");
        return left; 
    }
}

AtribExp expresion_simple() :
{
    System.out.println("Entrando en expresion_simple");
    AtribExp left, right;
    Token op;
    boolean isConst;
}
{
    left = termino()
    { 
        isConst = left.isConst; 
    }
    (
        LOOKAHEAD(2) (op = <tPLUS> | op = <tMINUS>)
        right = termino()
        {
            /**
             * Lanzar una excepción si los 2 operandos no son del mismo tipo.
             */
            if (left.type != right.type) {
                System.err.println(
                    "Error: Los operandos para el operador '" + op.image 
                    + "' deben ser del mismo tipo. Se evaluó: left.type = " + left.type 
                    + " con valor '" + left.image + "', right.type = " + right.type 
                    + " con valor '" + right.image + "'."
                );
            }

            /**
             * Lanzar una excepción si los 2 operandos no son de tipo integer.
             */
            if (left.type != Symbol.Types.INT || right.type != Symbol.Types.INT) {
                System.err.println(
                    "Error: Los operadores de expresiones requieren valores enteros. Se evaluó: "
                    + "left.type = " + left.type + " con valor '" + left.image 
                    + "', right.type = " + right.type + " con valor '" + right.image + "'. Al menos uno de ellos no es booleano."
                );
            }
            
            isConst = isConst && right.isConst;
            
            /**
             * Evaluar la expresión simple si los 2 terminos son constantes.
             */
            if (isConst) {
                int l = Integer.parseInt(left.image);
                int r = Integer.parseInt(right.image);
                
                left.image = op.image.equals("+") ? String.valueOf(l + r): String.valueOf(l - r);
            } else {
                left.image = null;
            }
            
            left.isConst = isConst;
        }
    )*
    { 
        System.out.println("Saliendo de expresion_simple");
        return left; 
    }
}


AtribExp relacion() :
{
    System.out.println("Entrando en relacion");
    AtribExp left, right = null;
    Token op = null;
}
{
    left = expresion_simple()
    (
        op = operador_relacional()
        right = expresion_simple()
        {
            /**
             * Lanzar una excepción si los 2 operandos no son del mismo tipo.
             */
            if (left.type != right.type) {
                System.err.println(
                    "Error: Los operandos para el operador '" + op.image 
                    + "' deben ser del mismo tipo. Se evaluó: left.type = " + left.type 
                    + " con valor '" + left.image + "', right.type = " + right.type 
                    + " con valor '" + right.image + "'."
                );
            }

            /**
             * Lanzar una excepción si los 2 operandos no son de tipo booleando y el operador los requiere.
             */
            if (op.image.matches("=|!=") && (left.type != Symbol.Types.BOOL || right.type != Symbol.Types.BOOL)) {
                System.err.println(
                    "Error: Los operandos para el operador '" + op.image 
                    + "' deben ser del mismo tipo. Se evaluó: left.type = " + left.type 
                    + " con valor '" + left.image + "', right.type = " + right.type 
                    + " con valor '" + right.image + "'."
                );
            }

            /**
             * Lanzar una excepción si los 2 operandos no son de tipo entero y el operador los requiere.
             */
            if (op.image.matches("<|<=|>|>=") && (left.type != Symbol.Types.INT || right.type != Symbol.Types.INT))
                System.err.println(
                    "Error: Los operadores relacionales requieren valores enteros. Se evaluó: "
                    + "left.type = " + left.type + " con valor '" + left.image 
                    + "', right.type = " + right.type + " con valor '" + right.image + "'."
                );
            
            boolean isConst = left.isConst && right.isConst;
            String val = null;
            
            /**
             * Evaluar la relacion si las 2 expresiones simples son constantes.
             */
            if (isConst) {
                boolean result = false;
                
                switch (left.type) {
                    case INT:
                        int l = Integer.parseInt(left.image);
                        int r = Integer.parseInt(right.image);
                        switch (op.image) {
                            case "=": result = (l == r); break;
                            case "!=": result = (l != r); break;
                            case "<": result = (l < r); break;
                            case "<=": result = (l <= r); break;
                            case ">": result = (l > r); break;
                            case ">=": result = (l >= r); break;
                        }
                        break;
                        
                    case BOOL:
                        boolean bl = Boolean.parseBoolean(left.image);
                        boolean br = Boolean.parseBoolean(right.image);
                        result = op.image.equals("=") ? (bl == br) : (bl != br);
                        break;
                }
                
                val = String.valueOf(result);
            }
            
            left = new AtribExp(Symbol.Types.BOOL, val, false, isConst);
        }
    )?
    { 
        System.out.println("Saliendo de relacion");
        return left;
    }
}

AtribExp expresion() :
{
    System.out.println("Entrando en expresion");
    AtribExp left, right;
    Token op;
    boolean isConst;
    boolean left_type, right_type;
}
{
    left = relacion()
    { 
        isConst = left.isConst; 
    }
    (
        (op = <tAND> | op = <tOR>)
        right = relacion()
        {
            /**
             * Lanzar una excepción si los 2 operandos no son del mismo tipo.
             */
            if (left.type != right.type) {
                System.err.println(
                    "Error: Los operandos para el operador '" + op.image 
                    + "' deben ser del mismo tipo. Se evaluó: left.type = " + left.type 
                    + " con valor '" + left.image + "', right.type = " + right.type 
                    + " con valor '" + right.image + "'."
                );
            }

            /**
             * Lanzar una excepción si los 2 operandos no son de tipo booleando.
             */
            if (left.type != Symbol.Types.BOOL || right.type != Symbol.Types.BOOL) {
                System.err.println(
                    "Error: Los operadores de expresiones requieren valores booleanos. Se evaluó: "
                    + "left.type = " + left.type + " con valor '" + left.image 
                    + "', right.type = " + right.type + " con valor '" + right.image + "'. Al menos uno de ellos no es booleano."
                );
            }
            
            isConst = isConst && right.isConst;
            
            /**
             * Evaluar la expresion si las 2 relaciones son constantes.
             */
            if (isConst) {
                boolean l = Boolean.parseBoolean(left.image);
                boolean r = Boolean.parseBoolean(right.image);
                
                left.image = op.image.equals("&") 
                    ? String.valueOf(l && r)
                    : String.valueOf(l || r);
            } else {
                left.image = null;
            }
            
            left.isConst = isConst;
        }
    )*
    { 
        System.out.println("Saliendo de expresion");
        return left; 
    }
}


void lista_una_o_mas_exps() :
{}
{
    ( expresion() )+
}

Token operador_multiplicativo() :
{
    Token t;
}
{
    t = <tTIMES>    { return t; }
  | t = <tDIVIDE>   { return t; }
  | t = <tMOD>      { return t; }
}

Token operador_relacional() :
{
    Token t;
}
{
    t = <tEQUAL>    { return t; }
  | t = <tNOTEQUAL> { return t; }
  | t = <tLT>       { return t; }
  | t = <tLE>       { return t; }
  | t = <tGT>       { return t; }
  | t = <tGE>       { return t; }
}
