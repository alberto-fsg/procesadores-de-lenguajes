//*****************************************************************
// File:   gcl.jj
// Author: Procesadores de Lenguajes-University of Zaragoza
// Date:   enero 25
// Coms:   compilar mediante "ant"
//*****************************************************************

///////////////////////////////////////////////////////////////////////////////
// 1. ANALIZADOR LÉXICO
///////////////////////////////////////////////////////////////////////////////

options {
    IGNORE_CASE = true;
    COMMON_TOKEN_ACTION = true;
}

PARSER_BEGIN(gcl)

package traductor;

import lib.symbolTable.*;
import lib.symbolTable.exceptions.*;

import java.util.*;

public class gcl {
    // tabla de símbolos global
	static SymbolTable st;
   
   public static void main(String[] args) {
	   gcl parser = null;
	   
	   try {
           st = new SymbolTable(); 

		   if(args.length == 0) {
			   parser = new gcl(System.in);
		   }
		   else {
			   parser = new gcl(new java.io.FileInputStream(args[0]));
		   }
		   //Programa es el símbolo inicial de la gramática
		   parser.Programa();
		   //...
		   System.out.println("***** Análisis terminado con éxito *****");
	   }
	   catch (java.io.FileNotFoundException e) {
		   System.err.println ("Fichero " + args[0] + " no encontrado.");
	   }
	   catch (TokenMgrError e) {
		   System.err.println("LEX_ERROR: " + e.getMessage());
	   }
	   catch (Exception e) {
			// Handle the exception
			System.err.println("An exception occurred: " + e.getMessage());
	   }
	}

    // Helper method to insert symbols into the table
    private static void insertSymbols(AtribTipo aT, boolean isRef, ArrayList<String> lVars) {
    for (String id : lVars) {
        try {
            Symbol sV = null;
            if (aT.isArray) { // Handle arrays
                sV = new SymbolArray(id, 0, aT.size-1, aT.type);
            } else { // Handle basic types
                switch(aT.type) {
                    case INT:
                        sV = new SymbolInt(id); break;
                    case BOOL:
                        sV = new SymbolBool(id); break;
                    case CHAR:
                        sV = new SymbolChar(id); break;
                    default:
                        System.out.println("Error: tipo no definido para variable \"" + id + "\".");
                }
            }
            if (sV != null) {
                // Optional: Handle 'ref' modifier here (if supported by Symbol classes)
                if (isRef) {
                    System.out.println("[REF] Parámetro por referencia: " + id);
                }
                st.insertSymbol(sV); // Insert into symbol table
                System.out.println("[TABLA] Insertado: " + sV.toString());
            }
        } catch (AlreadyDefinedSymbolException e) {
            System.out.println("Error: Identificador ya existe: \"" + id + "\"");
        }
    }
}
}
PARSER_END(gcl)

// sacado de https://cs.lmu.edu/~ray/notes/javacc/
// "preprocesado"
SKIP: {
  " "
| "\t"
| "\n"
| "\r"
| <"--" (~["\n","\r"])* ("\n"|"\r")> // comentarios monolínea
}

// auxiliares
TOKEN : {
	< #LETTER: (["a"-"z","A"-"Z"]) >
|	< #DIGIT: ["0"-"9"] >
}

// todos los tokens de gcl
// tOTHER no es léxicamente válido, el resto de tokens si
TOKEN : {
     // palabras reservadas
     < tPROGRAMA: "Programa" >
|    < tREF: "ref" > 
|    < tINTEGER: "entero" >
|    < tCHARACTER: "caracter" >
|    < tBOOLEAN: "booleano" >
|    < tSELECTION: "Sel" >
|    < tEND_SELECTION: "Fsel" >
|    < tCASE: "caso" >
|    < tITERATION: "Mq" >
|    < tEND_ITERATION: "FMq" >
|    < tBEGGINING: "Principio" >
|    < tEND: "Fin" >
|    < tTRUE: "verdadero" >
|    < tFALSE: "falso" >
|    < tOTHERWISE: "dlc" >
|    < tABANDONAR: "abandonar" > 
|    < tNADA: "nada" >
    
     // literales
|    < tSTRING: "\"" ( ("\"\"") | ~["\""] )* "\"" >
|    < tCHAR: "'''" | "'" ~[] "'" > 
|    < tCONSTANT: ( <DIGIT> )+ >
    
     // operadores y símbolos
|    < tCOMA: "," >
|    < tSEMICOLON: ";" >
|    < tUNDERSCORE: "_" >
|    < tASSIGN: ":=" >
|    < tOPENING_SQUARE_BRACKET: "[" >
|    < tCLOSING_SQUARE_BRACKET: "]" >
|    < tCOLON: ":" >
|    < tOPENING_PARENTHESIS: "(" >
|    < tCLOSING_PARENTHESIS: ")" >
|    < tPLUS: "+" >
|    < tMINUS: "-" >
|    < tTIMES: "*" >
|    < tDIVIDE: "/" >
|    < tMOD: "mod" >
|    < tAND: "&" >
|    < tOR: "|" >
|    < tNOT: "!" >
|    < tGT: ">" >
|    < tGE: ">=" >
|    < tLT: "<" >
|    < tLE: "<=" >
|    < tEQUAL: "=" >
|    < tNOTEQUAL: "<>" >
|    < tDOT: "." >

|    < tIDENTIFICADOR: ( <LETTER> | "_" ) ( <LETTER> | <DIGIT> | "_" )* >

|    < tOTHER: ~[] >  // tOTHER no es léxicamente válido, el resto de tokens si
}

// deprecado al parecer, https://parsers.org/javacc21/token-hooks-revisited/
TOKEN_MGR_DECLS : {
    public static void CommonTokenAction(Token t) {
        // Print ALL tokens with their type and image
        System.out.printf("(%d,%d): %s \"%s\"%n", 
            t.beginLine, t.beginColumn, getTokenName(t.kind), t.image);
    }

    // Get token name dynamically using reflection
    private static String getTokenName(int kind) {
        try {
            // Use reflection to find the token name in gclConstants
            java.lang.reflect.Field[] fields = gclConstants.class.getDeclaredFields();
            for (java.lang.reflect.Field field : fields) {
                if (field.getType() == int.class 
                        && field.getInt(null) == kind 
                        && field.getName().startsWith("t")) {
                    return field.getName(); // e.g., "tIDENTIFICADOR", "tASSIGN"
                }
            }
            return "UNKNOWN_TOKEN";
        } catch (IllegalAccessException e) {
            return "ERROR: " + e.getMessage();
        }
    }
}










///////////////////////////////////////////////////////////////////////////////
// 2. ANALIZADOR SINTÁCTICO
///////////////////////////////////////////////////////////////////////////////

/** Programa */

void Programa() :
{}
{
    < tPROGRAMA >
    < tIDENTIFICADOR >
    ( LOOKAHEAD(3) declaracion_variables() )*
    ( declaracion_procs_funcs() )*
    < tBEGGINING >
    instrucciones()
    < tEND >
}

///////////////////////////////////////////////////////////////////////////////

/** Tipos */

AtribTipo tipo() :
{
    AtribTipo aT;
}
{
    (
        LOOKAHEAD(2) 
        aT = tipoArray() 
        | aT = tipoEscalar()
    )
    { return aT; }
}

AtribTipo tipoEscalar() :
{}
{
    <tINTEGER>      { return new AtribTipo(Symbol.Types.INT ); }
    | <tCHARACTER>  { return new AtribTipo(Symbol.Types.CHAR); }
    | <tBOOLEAN>    { return new AtribTipo(Symbol.Types.BOOL); }
}

AtribTipo tipoArray() :
{
    Symbol.Types t;
    AtribExp aE;
}
{
    (
        <tCHARACTER> { t = Symbol.Types.CHAR; }
        | <tBOOLEAN> { t = Symbol.Types.BOOL; }
        | <tINTEGER> { t = Symbol.Types.INT; }
    )
    <tOPENING_SQUARE_BRACKET>
    aE = expresion_simple()
    <tCLOSING_SQUARE_BRACKET>
    {
        return new AtribTipo(t, Integer.parseInt(aE.image));
    }
}

///////////////////////////////////////////////////////////////////////////////

/** Declaración variables */

void declaracion_variables() :
{}
{
    ( LOOKAHEAD(3) declaracion() )+
}

ArrayList<String> lista_ids() :
{
    ArrayList<String> lVars = new ArrayList<String>();
    Token t;
}
{
    t = <tIDENTIFICADOR>
    { lVars.add(t.image); }
    (
        <tCOMA>
        t = <tIDENTIFICADOR>
        { lVars.add(t.image); }
    )*
    { return lVars; }
}

void declaracion() :
{
    AtribTipo aT;
    ArrayList<String> lVars;
}
{
    aT = tipo()
    lVars = lista_ids()
    <tSEMICOLON>
    {
        for(String id : lVars) {
            try {
                Symbol sV = null;
                if(aT.isArray) {
                    sV = new SymbolArray(id, 0, aT.size-1, aT.type);
                } else {
                    switch(aT.type) {
                        case INT:
                            sV = new SymbolInt(id); break;
                        case BOOL:
                            sV = new SymbolBool(id); break;
                        case CHAR:
                            sV = new SymbolChar(id); break;
                        default:
                            System.out.println("Error: tipo no definido para variable \"" +
                                id + "\".");
                    }
                }
                if(sV != null) {
                    st.insertSymbol(sV);
                    System.out.println("[TABLA DE SÍMBOLOS] Insertado " + sV.toString());
                }
            } catch(AlreadyDefinedSymbolException e) {
                System.out.println("Error: ya existe variable con identificador \"" +
                    id + "\".");
            }
        }
    }
}

///////////////////////////////////////////////////////////////////////////////

/** Expresiones */

// orden de prioridad de expansión (cuanto más izquierda, más prioridad)
// primario - factor - termino - expresion_simple - relacion - expresion

AtribExp primario() :
{
    Token t;
    AtribExp ae;
    Symbol s;
}
{
    {
        System.out.println("Entering primario");
    }
    (
        // Caso: llamada a función
        LOOKAHEAD(2)
        t = <tIDENTIFICADOR>
        <tOPENING_PARENTHESIS>
        parametros_llamada_funcion()
        <tCLOSING_PARENTHESIS>
        {
            try {
                s = st.getSymbol(t.image);
            } catch(SymbolNotFoundException e) {
                System.out.println("Error: El identificador '" + t.image + "' no está definido.");
                System.out.println("Exiting primario due to undefined symbol");
                return new AtribExp(Symbol.Types.UNDEFINED, null, false, false);
            }
            if (s.type != Symbol.Types.FUNCTION) {
                System.out.println("Error: Se esperaba que '" + t.image + "' fuera una función, pero se encontró: " + s.type + ".");
                System.out.println("Exiting primario due to symbol not being a function");
                return new AtribExp(Symbol.Types.UNDEFINED, null, false, false);
            }
            System.out.println("Exiting primario via function call case");
            return new AtribExp(s.type, null, false, false);
        }
    |
        // Caso: acceso a array
        LOOKAHEAD(2)
        t = <tIDENTIFICADOR>
        <tOPENING_SQUARE_BRACKET>
        ae = expresion()
        <tCLOSING_SQUARE_BRACKET>
        {
            try {
                s = st.getSymbol(t.image);
            } catch(SymbolNotFoundException e) {
                System.out.println("Error: El identificador '" + t.image + "' no está definido.");
                System.out.println("Exiting primario due to undefined symbol");
                return new AtribExp(Symbol.Types.UNDEFINED, null, false, false);
            }
            if (s.type != Symbol.Types.ARRAY) {
                System.out.println("Error: Se esperaba que '" + t.image + "' fuera un arreglo, pero se encontró: " + s.type + ".");
                System.out.println("Exiting primario due to symbol not being an array");
                return new AtribExp(Symbol.Types.UNDEFINED, null, false, false);
            }
            if (ae.type != Symbol.Types.INT) {
                System.out.println("Error: Se esperaba que el índice del arreglo fuera un entero, pero se evaluó: tipo " 
                    + ae.type + " con valor '" + ae.image + "'.");
            }
            SymbolArray arraySymbol = (SymbolArray) s;
            Symbol.Types elemType = arraySymbol.baseType;  
            System.out.println("Exiting primario via array access case");
            return new AtribExp(elemType, null, false, ae.isConst && s.constant);
        }
    |
        // Caso: identificador
        t = <tIDENTIFICADOR>
        {
            try {
                s = st.getSymbol(t.image);
                String image = t.image;
                System.out.println("Exiting primario via identifier case");
                return new AtribExp(s.type, image, true, s.constant);
            } catch (SymbolNotFoundException e) {
                System.out.println("Error: El identificador '" + t.image + "' no está definido.");
                System.out.println("Exiting primario due to undefined identifier");
                return new AtribExp(Symbol.Types.UNDEFINED, null, false, false);
            }
        }
    |
        t = <tCONSTANT>
        { 
            System.out.println("Exiting primario via integer constant case");
            return new AtribExp(Symbol.Types.INT, t.image, false, true); 
        }
    |
        t = <tCHAR>
        { 
            System.out.println("Exiting primario via char constant case");
            return new AtribExp(Symbol.Types.CHAR, t.image, false, true); 
        }
    |
        t = <tSTRING>
        { 
            System.out.println("Exiting primario via string constant case");
            return new AtribExp(Symbol.Types.STRING, t.image, false, true); 
        }
    |
    t = <tTRUE>
                { 
            System.out.println("Exiting primario via boolean true constant case");
            return new AtribExp(Symbol.Types.BOOL, t.image, false, true); 
        }
    | 
    t = <tFALSE>
        { 
            System.out.println("Exiting primario via boolean false constant case");
            return new AtribExp(Symbol.Types.BOOL, t.image, false, true); 
        }
    )
    {
        System.out.println("Exiting primario via default (unreachable?)");
        return new AtribExp(Symbol.Types.UNDEFINED, null, false, false);
    }
}


AtribExp factor() :
{
    System.out.println("Entering factor");
    Token op = null;
    AtribExp ae;
}
{
    ( op = <tNOT> | op = <tPLUS> | op = <tMINUS> )?
    (
        <tOPENING_PARENTHESIS> ae = expresion() <tCLOSING_PARENTHESIS>
    |
        ae = primario()
    )
    {
        if (op != null) {
            switch (op.image) {
                case "!":
                    if (ae.type != Symbol.Types.BOOL) {
                        throw new TypesDontMatchException("Error: El operador '!' requiere un valor booleano, pero se evaluó: tipo " + ae.type + " con valor '" + ae.image + "'.");
                    }
                    if (ae.isConst) {
                        boolean val = Boolean.parseBoolean(ae.image);
                        System.out.println("Exiting factor after ! operator with const result");
                        return new AtribExp(Symbol.Types.BOOL, String.valueOf(!val), false, true);
                    }
                    System.out.println("Exiting factor after ! operator");
                    return new AtribExp(Symbol.Types.BOOL, null, false, false);
                case "+":
                    if (ae.type != Symbol.Types.INT) {
                        throw new TypesDontMatchException("Error: El operador '+' requiere un valor entero, pero se evaluó: tipo " + ae.type + " con valor '" + ae.image + "'.");
                    }
                    System.out.println("Exiting factor after + operator");
                    return ae;
                case "-":
                    if (ae.type != Symbol.Types.INT) {
                        throw new TypesDontMatchException("Error: El operador '-' requiere un valor entero, pero se evaluó: tipo " + ae.type + " con valor '" + ae.image + "'.");
                    }
                    if (ae.isConst) {
                        int val = Integer.parseInt(ae.image);
                        System.out.println("Exiting factor after - operator with const result");
                        return new AtribExp(Symbol.Types.INT, String.valueOf(-val), false, true);
                    }
                    System.out.println("Exiting factor after - operator");
                    return new AtribExp(Symbol.Types.INT, null, false, false);
            }
        }
        System.out.println("Exiting factor with primary expression");
        return ae;
    }
}

AtribExp termino() :
{
    System.out.println("Entering termino");
    AtribExp left, right;
    Token op;
}
{
    left = factor()
    (
        op = operador_multiplicativo()
        right = factor()
        {
            if (left.type != Symbol.Types.INT || right.type != Symbol.Types.INT) {
                throw new TypesDontMatchException("Error: Los operadores multiplicativos requieren valores enteros. Se evaluó: left.type = " + left.type + " con valor '" + left.image + "', right.type = " + right.type + " con valor '" + right.image + "'.");
            }
            boolean isConst = left.isConst && right.isConst;
            String val = null;
            if (isConst) {
                int l = Integer.parseInt(left.image);
                int r = Integer.parseInt(right.image);
                switch (op.image) {
                    case "*": val = String.valueOf(l * r); break;
                    case "/": val = r != 0 ? String.valueOf(l / r) : "0"; break;
                    case "%": val = String.valueOf(l % r); break;
                }
            }
            left = new AtribExp(Symbol.Types.INT, val, false, isConst);
        }
    )*
    { 
        System.out.println("Exiting termino");
        return left; 
    }
}

AtribExp expresion_simple() :
{
    System.out.println("Entering expresion_simple");
    AtribExp left, right;
    Token op;
    boolean isConst;
}
{
    left = termino()
    { isConst = left.isConst; }
    (
        ( op = <tPLUS> | op = <tMINUS> )
        right = termino()
        {
            if (left.type != Symbol.Types.INT || right.type != Symbol.Types.INT) {
                throw new TypesDontMatchException("Error: Los operadores aditivos requieren valores enteros. Se evaluó: left.type = " + left.type + " con valor '" + left.image + "', right.type = " + right.type + " con valor '" + right.image + "'.");
            }
            isConst = isConst && right.isConst;
            if (isConst) {
                int l = Integer.parseInt(left.image);
                int r = Integer.parseInt(right.image);
                left.image = op.image.equals("+") ? String.valueOf(l + r) : String.valueOf(l - r);
            } else {
                left.image = null;
            }
            left.isConst = isConst;
        }
    )*
    { 
        System.out.println("Exiting expresion_simple");
        return left; 
    }
}

AtribExp relacion() :
{
    System.out.println("Entering relacion");
    AtribExp left, right = null;
    Token op = null;
}
{
    left = expresion_simple()
    (
        op = operador_relacional()
        right = expresion_simple()
        {
            if (op.image.matches("=|!=")) {
                if (left.type != right.type) {
                    throw new TypesDontMatchException("Error: Los operandos para el operador '" + op.image + "' deben ser del mismo tipo. Se evaluó: left.type = " + left.type + " con valor '" + left.image + "', right.type = " + right.type + " con valor '" + right.image + "'.");
                }
            } else {
                if (left.type != Symbol.Types.INT || right.type != Symbol.Types.INT) {
                    throw new TypesDontMatchException("Error: Los operadores relacionales requieren valores enteros. Se evaluó: left.type = " + left.type + " con valor '" + left.image + "', right.type = " + right.type + " con valor '" + right.image + "'.");
                }
            }
            
            boolean isConst = left.isConst && right.isConst;
            String val = null;
            if (isConst) {
                boolean result = false;
                switch (left.type) {
                    case INT:
                        int l = Integer.parseInt(left.image);
                        int r = Integer.parseInt(right.image);
                        switch (op.image) {
                            case "=": result = (l == r); break;
                            case "!=": result = (l != r); break;
                            case "<": result = (l < r); break;
                            case "<=": result = (l <= r); break;
                            case ">": result = (l > r); break;
                            case ">=": result = (l >= r); break;
                        }
                        break;
                    case BOOL:
                        boolean bl = Boolean.parseBoolean(left.image);
                        boolean br = Boolean.parseBoolean(right.image);
                        result = (op.image.equals("=")) ? (bl == br) : (bl != br);
                        break;
                }
                val = String.valueOf(result);
            }
            left = new AtribExp(Symbol.Types.BOOL, val, false, isConst);
        }
    )?
    { 
        System.out.println("Exiting relacion");
        return right == null ? left : left; 
    }
}

AtribExp expresion() :
{
    System.out.println("Entering expresion");
    AtribExp left, right;
    Token op;
    boolean isConst;
}
{
    left = relacion()
    { isConst = left.isConst; }
    (
        ( op = <tAND> | op = <tOR> )
        right = relacion()
        {
            if (left.type != Symbol.Types.BOOL || right.type != Symbol.Types.BOOL) {
                throw new TypesDontMatchException("Error: Los operadores lógicos requieren valores booleanos. Se evaluó: left.type = " + left.type + " con valor '" + left.image + "', right.type = " + right.type + " con valor '" + right.image + "'. Al menos uno de ellos no es booleano.");
            }
            isConst = isConst && right.isConst;
            if (isConst) {
                boolean l = Boolean.parseBoolean(left.image);
                boolean r = Boolean.parseBoolean(right.image);
                left.image = op.image.equals("&") 
                    ? String.valueOf(l && r) 
                    : String.valueOf(l || r);
            } else {
                left.image = null;
            }
            left.isConst = isConst;
        }
    )*
    { 
        System.out.println("Exiting expresion");
        return left; 
    }
}

void lista_una_o_mas_exps() :
{}
{
    ( expresion() )+
}

Token operador_multiplicativo() :
{
    Token t;
}
{
    t = <tTIMES>    { return t; }
  | t = <tDIVIDE>   { return t; }
  | t = <tMOD>      { return t; }
}

Token operador_relacional() :
{
    Token t;
}
{
    t = <tEQUAL>    { return t; }
  | t = <tNOTEQUAL> { return t; }
  | t = <tLT>       { return t; }
  | t = <tLE>       { return t; }
  | t = <tGT>       { return t; }
  | t = <tGE>       { return t; }
}

///////////////////////////////////////////////////////////////////////////////

/** Asignación */

void asignacion() :
{ System.out.println("Asignacion"); }
{
    < tIDENTIFICADOR >
    ( < tOPENING_SQUARE_BRACKET > expresion_simple() < tCLOSING_SQUARE_BRACKET > )?
    < tASSIGN >
    expresion()
    < tSEMICOLON >
}

///////////////////////////////////////////////////////////////////////////////

/** Funciones y procedimientos */

void declaracion_procs_funcs() :
{ System.out.println("Declaración de procedimientos/funciones"); }
{
    ( declaracion_proc() | declaracion_func() )+
}

void declaracion_proc() :
{ System.out.println("Declaración de procedimiento"); }
{
    <tIDENTIFICADOR>
    < tOPENING_PARENTHESIS >
    ( parametros_declaraciones() )*
    < tCLOSING_PARENTHESIS >
    ( LOOKAHEAD(3) declaracion_variables() )*
    ( declaracion_procs_funcs() )*
    < tBEGGINING >
    instrucciones()
    < tEND >
}

void declaracion_func() :
{ System.out.println("Declaración de función"); }
{
    tipoEscalar()
    <tIDENTIFICADOR>
    < tOPENING_PARENTHESIS >
    ( parametros_declaraciones() )*
    < tCLOSING_PARENTHESIS >
    ( LOOKAHEAD(3) declaracion_variables() )*
    ( declaracion_procs_funcs() )*
    < tBEGGINING >
    instrucciones()
    < tEND >
}

void llamar_funcion() :
{ System.out.println("Llamar función"); }
{
    < tIDENTIFICADOR >
    < tOPENING_PARENTHESIS >
    parametros_llamada_funcion()
    < tCLOSING_PARENTHESIS >
    < tSEMICOLON >
}

void parametros_llamada_funcion() :
{ System.out.println("Parámetros llamada función"); }
{
    ( expresion() )?
    (<tCOMA> expresion() )*
    //( LOOKAHEAD(2) <tIDENTIFICADOR> <tOPENING_SQUARE_BRACKET> expresion_simple() <tCLOSING_SQUARE_BRACKET> | <tIDENTIFICADOR> | expresion() )?
    //(<tCOMA>  (LOOKAHEAD(2) <tIDENTIFICADOR> <tOPENING_SQUARE_BRACKET> expresion_simple() <tCLOSING_SQUARE_BRACKET> | <tIDENTIFICADOR> | expresion() ))*
}

// --- Instrucciones ----------------------------------------------------------
void instrucciones() :
{ System.out.println("Instrucciones"); }
{
    ( instruccion() )*
}

void instruccion() :
{ System.out.println("Instrucción"); }
{
    LOOKAHEAD(2)
    llamar_funcion()
|   asignacion()
|   seleccion()
|   bucle()
|   declaracion_procs_funcs()
|   < tABANDONAR > < tSEMICOLON>
|   < tNADA > < tSEMICOLON>
}

void seleccion() :
{ System.out.println("Selección"); }
{
    < tSELECTION > 
    ( < tCASE > expresion() < tCOLON > instrucciones() )+
    [ < tOTHERWISE > < tCOLON > instrucciones() ]
    < tEND_SELECTION >
}

void bucle() :
{ System.out.println("Bucle"); }
{
    < tITERATION >
    expresion()
    instrucciones()
    < tEND_ITERATION >
}

void parametros_declaraciones() :
{   
    System.out.println("Parámetros declaraciones"); 
    AtribTipo aT;
    ArrayList<String> lVars;
    boolean isRef = false;
}
{
    // First parameter group
    aT = tipo()
    ( <tREF> { isRef = true; } )? // Capture 'ref' keyword
    lVars = lista_ids()
    {
        insertSymbols(aT, isRef, lVars); // Insert first group
        isRef = false; // Reset for next groups
    }
    // Additional parameter groups (separated by ';')
    (
        <tSEMICOLON>
        aT = tipo()
        ( <tREF> { isRef = true; } )? 
        lVars = lista_ids()
        {
            insertSymbols(aT, isRef, lVars); // Insert subsequent groups
            isRef = false;
        }
    )*
}
