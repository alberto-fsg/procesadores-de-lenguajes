ArrayList<String> listaIDs() :
{
    Token             token_id;
    ArrayList<String> ids = new ArrayList<String>();
}
{
    token_id = <tIDENTIFICADOR>    { ids.add(token_id.image); }
    (
      <tCOMA>
      token_id = <tIDENTIFICADOR>  { ids.add(token_id.image); }
    )*
    { return ids; }
}

void declaracionVariables() :
{
    AtribTipo         tipo_variables;
    ArrayList<String> ids;
}
{
    tipo_variables = tipo()
    ids            = listaIDs() 
    <tSEMICOLON>
    {
        declararVariablesGeneradorCodigo(tipo_variables, ids);
    }
}

CodeBlock asignacionVariable() :
{
    Token token_id;
    AtribExp index_expr = null; 
    AtribExp value_expr;
    CodeBlock c = new CodeBlock();
}
{
    token_id = <tIDENTIFICADOR>
    ( 
      <tOPENING_SQUARE_BRACKET>
      index_expr = expresionSimple()
      <tCLOSING_SQUARE_BRACKET>
    )?
    <tASSIGN>
    value_expr = expresion()
    <tSEMICOLON>
    {
        asignacionVariableGeneradorCodigo(c, token_id.image, index_expr, value_expr);
        return c;
    }
}
